config {
  type: "view",
  schema: "query"
}

WITH
  person_in_pipedrive_raw AS (
    SELECT DISTINCT *
    FROM ${ref('raw_customer_person')}
  ),

  customer_in_pipedrive_raw AS (
    SELECT DISTINCT * EXCEPT(row_number)
    FROM (
      SELECT *,
          ROW_NUMBER() OVER (PARTITION BY customer_id ORDER BY last_update DESC) AS row_number
      FROM person_in_pipedrive_raw
      ) t
    WHERE t.row_number = 1
  ), 

  customer_in_lead_raw AS (
    SELECT DISTINCT * EXCEPT(row_number)
    FROM (
      SELECT *,
          ROW_NUMBER() OVER (PARTITION BY customer_id ORDER BY create_date,phone DESC) AS row_number
      FROM (SELECT DISTINCT customer_id,name,email,phone,title,create_date FROM ${ref('raw_lead')})
      ) t
    WHERE t.row_number = 1
  )

-- Bước 1: Lấy tất cả dữ liệu từ customer_in_pipedrive_raw mà không có trong customer_in_lead_raw
SELECT
    p.customer_id,
    COALESCE(NULLIF(TRIM(p.name), ''), NULLIF(TRIM(l.name), '')) AS name,
    NULLIF(TRIM(p.email), '') AS email,
    COALESCE(NULLIF(TRIM(l.phone), ''), NULLIF(TRIM(p.phone), '')) AS phone,
    p.birthday AS birthday,
    COALESCE(NULLIF(TRIM(p.title), ''), NULLIF(TRIM(l.title), '')) AS title,
    NULLIF(TRIM(p.position), '') AS position,
    p.last_update,
    p.create_date,
    p.last_activity_date,
    p.person_id,
    p.org_id,
    1 AS status -- Đã có trong customer_in_pipedrive_raw nên status = 1
FROM customer_in_pipedrive_raw p
LEFT JOIN customer_in_lead_raw l ON p.customer_id = l.customer_id
WHERE l.customer_id IS NULL

UNION ALL

-- Bước 2: Lấy tất cả dữ liệu từ customer_in_lead_raw và dữ liệu tương ứng từ customer_in_pipedrive_raw
SELECT
    COALESCE(p.customer_id, l.customer_id) AS customer_id,
    COALESCE(NULLIF(TRIM(p.name), ''), NULLIF(TRIM(l.name), '')) AS name,
    NULLIF(TRIM(l.email), '') AS email,
    COALESCE(NULLIF(TRIM(l.phone), ''), NULLIF(TRIM(p.phone), '')) AS phone,
    p.birthday AS birthday,
    COALESCE(NULLIF(TRIM(p.title), ''), NULLIF(TRIM(l.title), '')) AS title,
    NULLIF(TRIM(p.position), '') AS position,
    p.last_update,
    COALESCE(p.create_date, l.create_date) AS create_date,
    p.last_activity_date,
    p.person_id,
    p.org_id,
    CASE 
        WHEN p.customer_id IS NOT NULL THEN 1
        ELSE 0 
    END AS status
FROM customer_in_lead_raw l
LEFT JOIN customer_in_pipedrive_raw p ON l.customer_id = p.customer_id



